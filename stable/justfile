# show the list of options
help:
	@just --list


# Figure out how many patches not applied from each queue
left:
	#!/usr/bin/env bash
	cd stable-queue
	LS_FILES=$(git ls-files -o)
	NUMBERS=$(echo "${LS_FILES}" | cut -f 1 -d '/' | cut -f 2 -d '-' | sort --version-sort | uniq)
	total=0
	echo "patches not committed:"
	for N in ${NUMBERS}; do
		uncommit=$(echo "${LS_FILES}" | grep ${N} | wc -l)
		printf "	%3s:	%4d\n" "${N}" "${uncommit}"
		total=$((uncommit + total))
	done
	printf "	total:	%4d\n" "${total}"


# commit patches in a stable queue for all kernel versions
commit:
	#!/usr/bin/env bash
	cd stable-queue
	NUMBERS=$(git ls-files -o | cut -f 1 -d '/' | cut -f 2 -d '-' | sort --version-sort | uniq)
	for N in ${NUMBERS}; do
		echo "../do_it_all ${N}"
		../do_it_all "${N}"
	done


